apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 29

    defaultConfig {
        applicationId "dev.freya.phoenix"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 29991
        versionName '2.91'

        buildConfigField 'int', 'VK_API_APP_ID', getProps("vk_app_id")
        buildConfigField 'String', 'FCM_SENDER_ID', getProps("fcm_sender_id")
        buildConfigField 'String', 'SERVICE_TOKEN', getProps("vk_service_token")
        buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("vk_client_secret")
        buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("youtube_dev_key")

        ndk {
            abiFilters 'x86', 'armeabi-v7a', 'arm64-v8a'
        }

    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            multiDexEnabled true
        }
    }
}

def getProps(String propName) {
    def propsFile = rootProject.file('build-config-fields.properties')

    if (propsFile.exists()) {
        def props = new Properties()

        FileInputStream fis
        try {
            fis = new FileInputStream(propsFile)
            props.load(fis)

            def value = props[propName]
            fis.close()
            return value
        } finally {
            if (fis != null) fis.close()
        }
    } else {
        throw new GradleException('File build-config-fields.properties not found')
    }
}

allprojects {
    repositories {
        maven {
            url "https://jitpack.io"
        }

        maven {
            url 'https://maven.google.com'
        }
        maven { url 'https://kotlin.bintray.com/kotlin-dev' }
    }
}

ext {
    retrofitLibraryVersion = '2.9.0'
    okhttpLibraryVersion = '4.7.2'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.browser:browser:1.3.0-alpha04"
    implementation 'androidx.webkit:webkit:1.3.0-rc01'
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.exifinterface:exifinterface:1.2.0"
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation files('libs/YouTubeAndroidPlayerApi.jar')
    implementation project(path: ':picasso')
    implementation project(path: ':mvpcore')
    implementation project(path: ':material-popup-menu')
    implementation project(path: ':ealvatag')
    implementation "com.squareup.retrofit2:retrofit:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitLibraryVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpLibraryVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLibraryVersion"
    implementation 'org.conscrypt:conscrypt-android:2.4.0'
    implementation 'com.google.firebase:firebase-database:19.3.1'
    implementation 'com.google.firebase:firebase-messaging:20.1.0'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.11.7'
    implementation 'com.google.android.exoplayer:exoplayer-hls:2.11.7'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta7'
    implementation 'com.airbnb.android:lottie:3.4.1'
    implementation 'androidx.biometric:biometric:1.0.1'
    implementation 'com.github.TutorialsAndroid:FilePicker:v8.0.19'
    implementation "com.github.skydoves:colorpickerpreference:2.0.0"
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'androidx.media:media:1.2.0-alpha04'
}

apply plugin: 'com.google.gms.google-services'
